# code: language=shellscript
local toplevel
if toplevel=$(git worktree list --porcelain | head -n1 | sd '^worktree ' '') ; then
  local name=${1:-pr}
  local root=${toplevel:h}/$name-worktrees/${toplevel:t}
  if [[ ! -d $root ]] ; then
    local branch
    if (( $#>=2 )); then
      branch=$2
    elif branch=$(git symbolic-ref HEAD --short) ; then
      branch=$branch+$name
    fi
    if [[ -z $branch ]]; then
      git worktree add $root --detach
    else
      git worktree add $root -b $branch ||
      git worktree add $root $branch
    fi
  fi
  local prefix="$(git rev-parse --show-prefix)"
  while ! cd "$root/$prefix"; do prefix=${prefix:h}; [[ -z "$prefix" ]] && break; done;
else
  echo 'not in git dir'
fi
