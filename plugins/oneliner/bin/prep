#!/usr/bin/env zsh

set -euo pipefail

wasrun=false
run() {
  wasrun=true
  echo '>' $@
  $@
}

if [[ -f package.json ]] &&
  jsPackageManager=$(jq -e -r '.packageManager // empty' package.json)
then
  run corepack prepare
  run ${jsPackageManager%@*} install
elif [[ -f yarn.lock ]]; then
  run yarn
elif [[ -f pnpm-lock.yaml ]]; then
  run pnpm install
elif [[ -f bun.lockb ]]; then
  run bun install
elif [[ -f package-lock.json ]]; then
  run npm install
elif [[ -f package.json ]]; then
  echo '# no lockfile found or packageManager defined, using npm'
  run npm install
fi

if [[ -f pdm.lock ]]; then
  run pdm install
  # generate a .direnv instead (if none exists)
  echo "# run \`eval $\(pdm venv activate)\` for virtualenv"
elif [[ -f poetry.lock ]]; then
  run poetry install
elif [[ -f pyproject.toml ]]; then
  echo unknown pyproject
  exit 1
fi

if [[ -f Cargo.toml ]]; then
  run cargo build
fi

if [[ -f Podfile ]]; then
  run pod install
fi

if ! $wasrun; then
  echo '# no supported manifest files found'
fi
