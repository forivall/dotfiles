#!/usr/bin/env zsh

set -eo pipefail

__plugin_dirname=${0:a:h:h}

local configArgs=()

local cdup=$(git rev-parse --show-cdup)

__git-addp-hasfiles() {
  ! git diff-files --quiet $@ ||
  ! git ls-files --other --directory --no-empty-directory --exclude-standard $@ | sed q1 > /dev/null
}
__git-addp-getitems_vars() {
  local OIFS=$IFS
  IFS=$'\n'
  items=(
    $(git diff --name-only $@)
    $(git ls-files --full-name --other --directory --no-empty-directory --exclude-standard $@)
  )
  IFS="$OIFS"
  term_menu_options=(--no-preview-border --preview "$__plugin_dirname/__git-addp-preview $cdup{} ${#items}")
}
__git-addp-dointeractive() {
  if git ls-files --error-unmatch $@ 2>/dev/null >/dev/null; then
    git "${configArgs[@]}" add -p $@
  else
    git "${configArgs[@]}" add $@
    local display_cmd=(git "${configArgs[@]}" diff --cached $@)
    local diff_filter=$(git config interactive.diffFilter)
    if [[ -n $diff_filter ]]; then
      $display_cmd | eval $diff_filter | eval $(git var GIT_PAGER)
    else
      $display_cmd | eval $(git var GIT_PAGER)
    fi
    echo Added $@
  fi
}
source $__plugin_dirname/__git-patch-filepicker \
  __git-addp-{hasfiles,getitems_vars,dointeractive} $@ &&

echo 'Nothing left to stage'

true
