#!/usr/bin/env zsh
# shellcheck disable=SC2168,SC2296,SC2299,SC2300

__plugin_dirname=${0:a:h:h}

args=()
pretty=()
files=()
dashdash=false
allfiles=false
reflog=false
stash=false
showdiff=true
forcediff=false
dry=false
navigate=${$(git config delta.navigate):-false}

while (( $# > 0 )); do
case "$1" in
  -h)
    git log -h | sd 'usage: git log' 'usage: git logp' | rg -v 'or: git show'
    echo '    --all-files           filter commits on files, but show all files'
    echo
    echo 'in the less pager, use j / k to navigate history backward / forward'
    exit
  ;;
  stash) stash=true; args+=(stash); pretty+=(--pretty=medium) ;&
  --walk-reflogs) reflog=true; shift; continue; ;;
  --patch|--p) forcediff=true; ;&
  --no-patch|--name-status|--name-only|--raw|--cumulative|--dirstat|--dirstat-by-file|--dirstat-by-file=*|--stat|--numstat|--shortstat|--summary|--compact-summary) showdiff=false; ;&
  --ignore-cr-at-eol|--ignore-space-at-eol|-b|--ignore-space-change|-w|--ignore-all-space|--ignore-blank-lines|--pretty=*)
    pretty+=($1); shift; continue; ;;
  --pretty) pretty+=($1 $2); shift; shift; continue; ;;
  --navigate) navigate=true; shift; continue; ;;
  --no-navigate) navigate=false; shift; continue; ;;
  --all-files) allfiles=true; shift; continue; ;;
  --dry) dry=true; shift; continue; ;;
  --) dashdash=true; args+=("$1"); shift; continue; ;;
  *)
    args+=("$1")
    if $dashdash || [[ -e $1 ]]; then
      files+=("$1")
    fi
    shift
    continue
  ;;
esac
break;
done

if $reflog; then
  refspecs="$(git reflog --pretty=format:%gd --max-count=100 "${args[@]}")"
else
  refspecs="$(git log --pretty=%h --max-count=100 "${args[@]}")"
fi

dashdash=""
$allfiles || dashdash+="${(q+@)files[@]} "

deltaOpts=()
if (( ${+commands[dark-mode]} )) && [[ $(dark-mode status) == off ]]; then
  deltaOpts+=(--light --syntax-theme ${DELTA_LIGHT_THEME:-GitHub})
fi

output=()
for commit in ${(f)refspecs}; do
  if $reflog; then
    local reflog_command="git reflog show --color -n1 ${(q+@)pretty[@]} ${(q+@)commit};"
    if $stash; then
      local commitbase="$commit^"
      if $showdiff || $forcediff; then
        reflog_command+="git diff --color --pretty= ${(q+@)commitbase} ${(q+@)commit} -- $dashdash | command delta "${deltaOpts[@]}""
      else
        reflog_command+="echo; git diff --color ${(q+@)commitbase} ${(q+@)pretty[@]} --pretty= ${(q+@)commit} -- $dashdash"
      fi
    else
      if $showdiff || $forcediff; then
        reflog_command+="git show --color --pretty= ${(q+@)commit} -- $dashdash | command delta "${deltaOpts[@]}""
      else
        reflog_command+="echo; git show --color ${(q+@)pretty[@]} --pretty= ${(q+@)commit} -- $dashdash"
      fi
    fi
    output+=("<($reflog_command)")
  else
    output+=("<(git show --color ${(q+@)pretty[@]} ${(q+@)commit} -- $dashdash| command delta "${deltaOpts[@]}")")
  fi
done

if $dry; then
  for c in "${output[@]}"; do
    echo ${${c#<\(}%)}
  done
else
  export COLUMNS
  if $navigate; then
    # replicate delta's navigate feture
    # https://github.com/dandavison/delta/blob/0.14.0/src/utils/bat/output.rs#L162
    # https://github.com/dandavison/delta/blob/0.14.0/src/features/navigate.rs#L62
    # but default delta config.
    if [[ ! -v LESSHISTFILE ]]; then
      LESSHISTFILE=~/.lesshst
      if [[ ! -e $LESSHISTFILE ]]; then
        unset LESSHISTFILE
      fi
    fi
    DELTA_HIST_FILE=$(mktemp)
    if [[ -v LESSHISTFILE && $LESSHISTFILE != "-" && $LESSHISTFILE != "/dev/null" ]]; then
      cat < $LESSHISTFILE > "$DELTA_HIST_FILE"
    else
      echo ".less-history-file:" > "$DELTA_HIST_FILE"
    fi
    echo ".search" >> "$DELTA_HIST_FILE"
    printf '"^(commit%s%s%s%s%s)\n' "|Δ" "|added:" "|removed:" "|renamed:" "|•" >> "$DELTA_HIST_FILE"
    LESSHISTFILE=$DELTA_HIST_FILE
    export LESSHISTFILE
  fi
  eval less -f +X -x2 -R --lesskey-src="${__plugin_dirname}/share/git-logp.lesskey" "${output[@]}"

  wait
  rm "$DELTA_HIST_FILE"
fi
